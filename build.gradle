plugins {
    id 'java'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

checkstyle {
    toolVersion = "10.14.0"
    ignoreFailures = false
    maxWarnings = 0
}


group = 'com.consartist'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.springframework:spring-instrument'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

ext.getAgentJars = {
    project.configurations.findAll {
        it.name == "compileClasspath" || it.name == "productionRuntimeClasspath" || it.name == "runtimeClasspath"
    }.collectMany {
        it.files
    }.findAll {
        it.name =~ /aspectjweaver-.*\.jar/ || it.name =~ /spring-instrument-.*\.jar/
    }.toSet()
}

ext.getJvmArgs = {
    project.getAgentJars().collect {
        "-javaagent:${it}".toString()
    }.toList().plus("--add-opens=java.base/java.lang=ALL-UNNAMED")
}

ext.getJvmArgsForBootBuildImageContainer = {
    project.getAgentJars().collect {
        "-javaagent:/workspace/BOOT-INF/lib/${it.name}".toString()
    }.toList().plus("--add-opens=java.base/java.lang=ALL-UNNAMED")
}

bootBuildImage {
    imageName = "consartist/blog-spring-cacheable"
    environment["BPE_DELIM_JAVA_TOOL_OPTIONS"] = " "
   	environment["BPE_APPEND_JAVA_TOOL_OPTIONS"] = project.getJvmArgsForBootBuildImageContainer().join(" ")
}

bootRun {
    jvmArgs = project.getJvmArgs()
}

test {
    jvmArgs = project.getJvmArgs()
}

tasks.named('test') {
    useJUnitPlatform()
}

task getJvmArgs {
    description = 'Prints the JVM arguments to the console. Useful for setting up Intellij run configurations.'
    doLast {
        println(project.getJvmArgs().join(" "))
    }
}